; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
; Non-commercial use only

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{67A5C703-09F1-4489-955B-5ED8F90B78F8}
AppName=Z21Dashboard
AppVersion=1.0.0
AppPublisher=Jesper Wachs
AppPublisherURL=https://github.com/J-Wachs/Z21Dashboard
AppSupportURL=https://github.com/J-Wachs/Z21Dashboard
AppUpdatesURL=https://github.com/J-Wachs/Z21Dashboard
DefaultDirName={autopf}\Z21Dashboard
UninstallDisplayIcon={app}\Z21Dashboard.exe
; Ensure license.txt exists in the root of your repository
LicenseFile=license.txt

; "ArchitecturesAllowed=x64compatible" specifies that Setup cannot run
; on anything but x64 and Windows 11 on Arm.
ArchitecturesAllowed=x64compatible
; "ArchitecturesInstallIn64BitMode=x64compatible" requests that the
; install be done in "64-bit mode" on x64 or Windows 11 on Arm,
; meaning it should use the native 64-bit Program Files directory and
; the 64-bit view of the registry.
ArchitecturesInstallIn64BitMode=x64compatible

DisableProgramGroupPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only).
;PrivilegesRequired=lowest

; --- IMPORTANT CHANGES FOR GITHUB ACTIONS ---
; We output the installer to a relative folder named "Output"
OutputDir=Output
OutputBaseFilename=Z21DashboardSetup
; --------------------------------------------

PrivilegesRequired=admin
SolidCompression=yes
WizardStyle=modern dynamic

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "danish"; MessagesFile: "compiler:Languages\Danish.isl"

[CustomMessages]
; Define the text for English (Fallback/Standard)
english.DeleteDataPrompt=Do you want to delete Z21Dashboard application data for all users on this computer?
; Define the text for Danish
danish.DeleteDataPrompt=Vil du slette Z21Dashboard applikationsdata for alle brugere p√• denne computer?

[Code]
procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
var
  FindRec: TFindRec;
  UsersPath, TargetPath: String;
  SystemDrive: String;
  DeleteUserSetting: Integer;
begin
  if CurUninstallStep = usUninstall then
  begin
    DeleteUserSetting := MsgBox(CustomMessage('DeleteDataPrompt'), mbConfirmation, MB_YESNO);
    // Find systemdrive
    SystemDrive := ExpandConstant('{sd}');
    UsersPath := SystemDrive + '\Users';

    if FindFirst(UsersPath + '\*', FindRec) then
    begin
      try
        repeat
          if ((FindRec.Attributes and FILE_ATTRIBUTE_DIRECTORY) <> 0) and
             (FindRec.Name <> '.') and
             (FindRec.Name <> '..') then
          begin
            // Remove the folder where WebView2 data is stored.
            TargetPath := UsersPath + '\' + FindRec.Name + '\AppData\Local\Z21DashboardApp';

            if DirExists(TargetPath) then
            begin
              DelTree(TargetPath, True, True, True);
            end;
            
            if (DeleteUserSetting = IDYES) then
            begin
              // Remove the folder where the user's widgets and locomotive etc. info.
              TargetPath := UsersPath + '\' + FindRec.Name + '\AppData\Roaming\Z21DashboardApp';
              
              if DirExists(TargetPath) then
              begin
                DelTree(TargetPath, True, True, True);
              end;
            end;
          end;
        until not FindNext(FindRec);
      finally
        FindClose(FindRec);
      end;
    end;
  end;
end;

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
; --- UPDATED TO USE THE "publish" FOLDER FROM GITHUB ACTIONS ---
; The build script creates the 'publish' folder in the root, so we reference it relatively.
Source: "publish\Z21Dashboard.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "publish\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; ---------------------------------------------------------------

[Icons]
Name: "{autoprograms}\Z21Dashboard"; Filename: "{app}\Z21Dashboard.exe"
Name: "{autodesktop}\Z21Dashboard"; Filename: "{app}\Z21Dashboard.exe"; Tasks: desktopicon

[Run]
Filename: "{app}\Z21Dashboard.exe"; Description: "{cm:LaunchProgram,Z21Dashboard}"; Flags: nowait postinstall skipifsilent
